apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "API_KEY", "\"" + getApiKey() + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main { java.srcDirs = ['src/main/kotlin'] }
        test { java.srcDirs = ['src/test/kotlin'] }
        androidTest { java.srcDirs = ['src/androidTest/kotlin'] }
    }
}

def getApiKey(){
    def apiKey
    try {
        Properties props = new Properties()
        props.load(new FileInputStream(new File('config/secrets.properties')))
        apiKey = props['API_KEY']
    } catch (FileNotFoundException e) {
        apiKey = "unknown"
    }
    return apiKey
}

dependencies {
    def dependency = rootProject.ext.dataDependencies
    def testDependency = rootProject.ext.dataTestDependencies
    implementation project(':domain')
    implementation "${dependency.kotlin}"
    implementation "${dependency.moshi}"
    implementation "${dependency.koin}"
    implementation "${dependency.retrofit}"
    implementation "${dependency.retrofitMoshi}"
    implementation "${dependency.okHttpLogger}"
    implementation "${dependency.arrowCoreData}"
    implementation "${dependency.coroutinesCore}"
    implementation "${dependency.room}"
    kapt "${dependency.roomCompiler}"
    testImplementation "${testDependency.jUnit}"
    testImplementation "${testDependency.jUnitExt}"
    testImplementation "${testDependency.kotlinStdlib}"
    testImplementation "${testDependency.kotlinTestJunit}"
    testImplementation "${testDependency.assertK}"
    testImplementation "${testDependency.mockitoKotlin}"
}
